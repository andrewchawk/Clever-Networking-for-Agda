\documentclass{report}

\usepackage{newunicodechar}
\usepackage{amsmath}
\usepackage{amssymb}
\usepackage{parskip}
% The coloring distracts the author.
\usepackage[bw]{agda}

% What is a good place for this crap?
\newunicodechar{⊤}{\ensuremath{\mathnormal{\top}}}
\newunicodechar{ℕ}{\ensuremath{\mathnormal{\mathbb{N}}}}
\newunicodechar{₁}{\ensuremath{\mathnormal{_1}}}
\newunicodechar{₂}{\ensuremath{\mathnormal{_2}}}
\newunicodechar{≅}{\ensuremath{\mathnormal{\cong}}}

\title{\AgdaModule{Network.ProtocolsAndPackets}: An Extensible and Expressive but Slightly Clever Agda Module for the Representation of Protocols and Packets for Networks}

\begin{document}
\maketitle{}

\begin{abstract}
The author presents and explains \AgdaModule{Network.ProtocolsAndPackets}, which defines Agda representations of network packets and connections.  Notable properties of \AgdaModule{Network.ProtocolsAndPackets} include extensibility, expressive typing for protocols and packets, real-world examples, and at least some degree of formal verification.  However, \AgdaModule{Network.ProtocolsAndPackets} \emph{cannot} be directly used to actually establish network connections in the real world; \AgdaModule{Network.ProtocolsAndPackets} is a \texttt{--safe} module and, therefore, does not refer to \AgdaDatatype{IO}, which would be necessary for such establishment.
\end{abstract}

\chapter{Boilerplate Stuff}
This chapter contains the declaration of the name of this module, some import statements, and a few pattern definitions.  These things would not fit too well in any other part of the document.

\section{Options}
First and foremost --- actually, at this point in time, this option is the only one --- this document is safe, i.e., does not make use of postulates or any such nonsense.

\begin{code}
{-# OPTIONS --safe #-}
\end{code}

\section{Module Declaration}
The Agda parts of this paper constitute an Agda module.  Some submodules also exist.

\begin{code}
module Network.ProtocolsAndPackets where
\end{code}

\section{Imported Packages}
The author wishes to \emph{not} rewrite \emph{all} utilities.

\begin{code}
open import Function
open import Relation.Binary.HeterogeneousEquality
open import Data.Fin using (Fin; toℕ)
open import Data.Fin.Patterns
open import Data.String using (String)
open import Data.Nat
open import Data.List
open import Data.Unit.Polymorphic
open import Relation.Nullary
open import Data.Maybe
open import Data.Bool
open import Data.Product
open import Data.List.Relation.Unary.All using (All)
open import Data.List.Relation.Unary.Any using (Any)
open import Level using (Level)
open import Data.These
\end{code}

\section{Patterns and Patterns and Patterns}
This section --- and module --- contains various pattern definitions which exist to enable the writing of definitions which are expressive \emph{and} short.

\begin{code}
module AdditionalFinPatterns where
  pattern 10F = Fin.suc 9F
  pattern 11F = Fin.suc 10F
  pattern 12F = Fin.suc 11F
  pattern 13F = Fin.suc 12F
  pattern 14F = Fin.suc 13F
  pattern 15F = Fin.suc 14F
  pattern 16F = Fin.suc 15F
  pattern 17F = Fin.suc 16F
  pattern 18F = Fin.suc 17F
  pattern 19F = Fin.suc 18F
  pattern 20F = Fin.suc 19F
  pattern 21F = Fin.suc 20F
  pattern 22F = Fin.suc 21F
  pattern 23F = Fin.suc 22F
  pattern 24F = Fin.suc 23F
  pattern 25F = Fin.suc 24F
  pattern 26F = Fin.suc 25F
  pattern 27F = Fin.suc 26F
  pattern 28F = Fin.suc 27F
  pattern 29F = Fin.suc 28F
  pattern 30F = Fin.suc 29F
  pattern 31F = Fin.suc 30F
  pattern 32F = Fin.suc 31F
  pattern 33F = Fin.suc 32F
  pattern 34F = Fin.suc 33F
  pattern 35F = Fin.suc 34F
  pattern 36F = Fin.suc 35F
  pattern 37F = Fin.suc 36F
  pattern 38F = Fin.suc 37F
  pattern 39F = Fin.suc 38F
  pattern 40F = Fin.suc 39F
  pattern 41F = Fin.suc 40F
  pattern 42F = Fin.suc 41F
  pattern 43F = Fin.suc 42F
  pattern 44F = Fin.suc 43F
  pattern 45F = Fin.suc 44F
  pattern 46F = Fin.suc 45F
  pattern 47F = Fin.suc 46F
  pattern 48F = Fin.suc 47F
  pattern 49F = Fin.suc 48F
  pattern 50F = Fin.suc 49F
\end{code}

\part{The Core Types}
To prevent name clashes, this part constitutes an Agda module.

\begin{code}
module Core where
\end{code}

\chapter{Representation of Protocols}
Formally, where \(P\) is a certain subset of networking protocols, \(P\) is characterized by being such that for all elements \(p\) of \(P\), \(p\) can be represented as the combination of a type for network addresses for \(p\), a type for network ports for \(p\), although \(p\) might not explicitly use network ports, and a type for \(p\) packets.  Also, any such \(p\) probably has a name, and the name might even be an abbreviation, which could be convenient.

\AgdaRecord{Protocol} is an extension of this representation; where \AgdaBound{p} is some \AgdaRecord{Protocol} \AgdaBound{l} value, the following statements hold:
\begin{itemize}
  \item \AgdaField{Protocol.name} \AgdaBound{p} is a long name of the \AgdaBound{p} protocol.
  \item \AgdaField{Protocol.shortName} \AgdaBound{p} is, optionally, an abbreviation of the long name of the \AgdaBound{p} protocol.
  \item If applicable, \AgdaField{Protocol.addressType} \AgdaBound{p} contains the type of network addresses which are used by the \AgdaBound{p} protocol.
  \item \AgdaField{Protocol.headerType} \AgdaBound{p} is the type of all headers for packets in the \AgdaBound{p} protocol.
  \item \AgdaField{Protocol.isSerialized} \AgdaBound{p} is \AgdaInductiveConstructor{true} if and only if all \AgdaBound{p} packets have serialized equivalents.
\end{itemize}

\begin{code}
  interleaved mutual
    record Protocol (l : Level) : Set (Level.suc l) where
      inductive
      field
        name : String
        shortName : Maybe String
        addressType : Maybe Set
        headerType : Set l
        isSerialized : Bool
\end{code}

\section{Can \AgdaRecord{Protocol} Unambiguously Represent \emph{Any} Network Protocol?}
\AgdaRecord{Protcol} is good for representing many sorts of protocols but may be incapable of unambiguously representing all imaginable protocols; the author is uncertain of a formal definition of ``network protocol'', and awareness of such a definition would facilitate defining a thing which resembles \AgdaRecord{Protocol} but an really be used to represent \emph{any} network protocol.  The author \emph{may} conduct some more research into the idea of network protocols but, in the meantime, thinks that \AgdaRecord{Protocol} should suffice for most purposes.

\chapter{Representation of Packets}
Without a representation of trasmissions, what is the usefulness of a system for representing protcols?  The author does not care and will define a datatype which facilitates representing the packets of any given protocol.

A packet \(p\) for a protocol \(P\) can be thought of as being a combination of the following attributes:
\begin{itemize}
  \item the \(P\) address of the sender of \(p\),
  \item the \(P\) address of the destination for \(p\),
  \item the payload of \(p\), and
  \item the actual content or payload of \(p\).
\end{itemize}

The address type and content type are protocol-specific.  Fortunately, with dependent types, defining such a type is a simple process.  This paper's definition of an abstract packet is as follows:

\begin{code}
    record UPacket {l : Level} (protocol : Protocol l) : Set (Level.suc l) where
      inductive
      field
        source
         destination : maybe (\ n -> n) ⊤ (Protocol.addressType protocol)
        payloadProtocol : Maybe (Protocol l)
        payload : maybe UPacket ⊤ payloadProtocol
\end{code}

For a given \AgdaRecord{UPacket} \AgdaBound{P} value \AgdaBound{p}, the following statements hold:
\begin{itemize}
  \item \AgdaField{UPacket.source} \AgdaBound{p} is the address of the sender of \AgdaBound{p}.
  \item \AgdaField{UPacket.destination} \AgdaBound{p} is the address of the recipient of \AgdaBound{p}.
  \item If applicable, \AgdaField{UPacket.payload} \AgdaBound{p} is the payload of \AgdaBound{p}, which adheres to the \AgdaField{UPacket.payloadProtocol} \AgdaBound{p} protocol.
\end{itemize}

\section{Lack of Verification}
The reader might have noticed that that \AgdaRecord{UPacket} has few verification fields; really, \AgdaRecord{UPacket} only verifies that packets have serialized versions when such serialized versions are really necessary.  Records for formal verifications will be defined soon.

\section{The Name}
``\AgdaRecord{UPacket}'' is an abbreviation of \emph{something}, but the author does not certainly remember the nature of the abbreviation.  However, ``unverified'' and ``unsafe'' are reasonable expansions of the ``U'' in ``\AgdaRecord{UPacket}''.

\chapter{Representation of Sets of Interrelated Protocols}
A \AgdaRecord{ProtocolSet} record is a combination of a list of protocols and some guarantees about interaction between these protocols.  \AgdaRecord{ProtocolSet} enables indicating that interaction between protocols is verified such that no non-strictly-positive nonsense is necessary.

Specifically, for any value \AgdaBound{ps} of type \AgdaRecord{ProtocolSet} \AgdaBound{l}, the following statements hold:
\begin{itemize}
  \item For any value \AgdaSymbol(\AgdaBound{protocol} \AgdaSymbol , \AgdaBound{possiblyProtocols}\AgdaSymbol) in \AgdaField{ProtocolSet.protocols} \AgdaBound{ps}, the following statements hold:
  \begin{itemize}
    \item If \AgdaBound{possiblyProtocols} is \AgdaInductiveConstructor{nothing}, then \emph{any} packet can encapsulate any packet of the \AgdaBound{protocol} protocol.
    \item If \AgdaBound{possiblyProtocols} is \AgdaInductiveConstructor{just} \AgdaBound{carrierProtocols}, then any \AgdaBound{carrierProtocols} packet can encapsulate any packet of the \AgdaBound{protocol} protocol.
  \end{itemize}
  \item \AgdaField{ProtocolSet.universalSerialize} \AgdaBound{ps} is, as indicated by the name, a universal packet serialization function for protocols which explicitly do serialization.  The use of ``universal'' also indicates that \emph{regardless of whether or not the \AgdaBound{P} protocol is actually allowed to carry the payload protocol}, \AgdaField{ProtocolSet.universalSerialize} \AgdaUnderscore{} \AgdaBound{P} \AgdaUnderscore{} \AgdaUnderscore{} \AgdaUnderscore{} is a serialized representation of the \AgdaBound{p} packet.
  \item \AgdaField{ProtocolSet.universalSerialize} \AgdaBound{ps} is, as \emph{this} name \emph{also} indicates, a universal packet \emph{de}serialization function for protocols which explicitly do serialization.  The use of ``universal'' \emph{also} indicates that \emph{regardless of whether or not the carrierprotocol is actually allowed to carry the payload protocol}, if and only if \AgdaBound{n} can be interpreted as representing some sort of appropriate packet, then \AgdaField{ProtocolSet.universalSerialize} \AgdaUnderscore{} \AgdaBound{n} contains a representation of the \AgdaBound{n} packet.
  \item \AgdaField{ProtocolSet.carrierUniqueness} \AgdaBound{ps} ensures that repeatedly defining the carriers of any single protocol is at worst\footnote{In this case, feasibility is a \emph{bad} thing!  Impossibility is ideal here.} infeasible.
  \item \AgdaField{ProtocolSet.carrierUniqueness} \AgdaBound{ps} ensures that for a given protocol \(p\) whose carrier protocols are explicitly enumerated, the list of carrier protocols for \(p\) contains no repetitions.   This type does not forbid much of consequence but does encourage clean definitions.
  \item \AgdaField{ProtocolSet.serialization-just} \AgdaBound{ps} ensures that if any protocol \(p\) supports a carrier protocol which demands serialization, then all \(p\) packets can be serialized.
  \item \AgdaField{ProtocolSet.standaloneAddresses} \AgdaBound{ps} ensures that any protocol which supports \emph{no} carrier protocols has some sort of address scheme.
  \item \AgdaField{ProtocolSet.deserialize-serialize-is-just} \AgdaBound{ps} ensures that the serialization function only outputs values which can be correctly deserialized by the deserialization function.
  \item \AgdaField{ProtocolSet.serialize-deserialize-is-just} \AgdaBound{ps} ensures that where for any packet's binary representation \(b\), the result of deserializing and reserializing \(b\) is the same old \(b\).
\end{itemize}

\begin{code}
  record ProtocolSet (l : Level) : Set (Level.suc l) where
    field
      protocols : List (Protocol l × Maybe (List (Protocol l)))
      universalSerialize :
        (P : Protocol l) ->
        (p : UPacket P) ->
        Protocol.isSerialized P ≅ true ->
        let serialization = Data.Maybe.map Protocol.isSerialized
                                           (UPacket.payloadProtocol p) in
        These (serialization ≅ just true) (Is-nothing serialization) ->
        Σ ℕ Fin
      universalDeserialize : Σ ℕ Fin -> Maybe (Σ (Protocol l) UPacket)
      carriedUniqueness :
        (n n2 : Fin (length protocols)) ->
        ¬ n ≅ n2 ->
        ¬ lookup protocols n ≅ lookup protocols n2
      carrierUniqueness :
        (n : Fin (length protocols)) ->
        (isJust : Is-just (proj₂ (lookup protocols n))) ->
        let carriers = to-witness isJust in
        (m m2 : Fin (length carriers)) ->
        ¬ m ≅ m2 ->
        ¬ lookup carriers m ≅ lookup carriers m2
      serialization-just :
        All (\ p -> (isJust : Is-just (proj₂ p))
                 -> Any (_≅_ true ∘ Protocol.isSerialized)
                        (to-witness isJust)
                 -> true ≅ Protocol.isSerialized (proj₁ p))
            protocols
      standaloneAddresses :
        All (\ p -> (isJust : Is-just (proj₂ p))
                 -> length (to-witness isJust) ≅ 0
                 -> Is-just (Protocol.addressType (proj₁ p)))
            protocols
      deserialize-serialize-is-just :
        (P : Protocol l) ->
        (p : UPacket P) ->
        (sp : Protocol.isSerialized P ≅ true) ->
        (spP : These (_ ≅ just true) (Is-nothing _)) ->
        just p ≅
        universalDeserialize (universalSerialize P p sp spP)
      serialize-deserialize-is-identical :
        (n : Σ ℕ Fin) ->
        (parsingSucceeds : Is-just (universalDeserialize n)) ->
        let p = to-witness parsingSucceeds in
        (sp : Protocol.isSerialized (proj₁ p) ≅ true)
        (spP : These _ _) ->
        universalSerialize _ (proj₂ p) sp spP ≅ n
\end{code}

\chapter{Representing Verified Packets}
For any value \AgdaBound{v} of type \AgdaRecord{Packet} \AgdaBound{p} \AgdaBound{s}, the following statements hold:
  \begin{itemize}
  \item \AgdaBound{v} is the combination of a representation of a packet of the \AgdaBound{p} protocol and some guarantees regarding this packet.  Some guarantees pertain to the interactions which are defined through \AgdaBound{s}.  Other guarantees pertain to\ldots{}\ well\ldots{}\ other stuff.
  \item \AgdaField{UPacket.rawPacket} \AgdaBound{v} represents the actual packet.
  \item \AgdaField{UPacket.mainProtocolExists} \AgdaBound{v} ensures that interactions with the main protocol of the packet are actually defined in the appropriate \AgdaRecord{ProtocolSet} record.
  \item \AgdaField{UPacket.carrierExists} \AgdaBound{v} ensures that \AgdaBound{s} actually supports the packet payload protocol.
  \item \AgdaField{UPacket.validCarrier-just} \AgdaBound{v} ensures that thr packet payload protocol actually supports being carried within packets for the \AgdaBound{p} protocol.
\end{itemize}

\begin{code}
  record Packet {l : Level}
                (protocol : Protocol l)
                (set : ProtocolSet l) :
                Set (Level.suc l)
    where
    field
      rawPacket : UPacket protocol
      mainProtocolExists :
        Any (_≅_ protocol)
            (Data.List.map proj₁ (ProtocolSet.protocols set))
      carrierExists :
        Σ (Fin (length (ProtocolSet.protocols set)))
          (\ n -> lookup (ProtocolSet.protocols set) n ≅
                 UPacket.payloadProtocol rawPacket)
      validCarrier-just  :
        (isJust : Is-just (proj₂ (lookup (ProtocolSet.protocols set)
                                         (proj₁ carrierExists)))) ->
        Any (_≅_ (UPacket.payloadProtocol rawPacket)) (to-witness isJust)
\end{code}

\part{Specific Protocols}

\begin{abstract}
Using \AgdaRecord{Protocol} and the like, the author defines expressive representations of protocols and corresponding packets.
\end{abstract}

\chapter{IPv4}
This chapter defines a representation of IPv4.

\begin{code}
module IPv4 where
\end{code}

RFC 791 describes version 4 of the Internet Protocol, which is also known as ``IPv4'', indicates that IPv4 addresses are 32-bit integers, and defines the IPv4 packet structure.  Specifically, RFC 791 indicates that any IPv4 packet \(p\) consists of the concatenation of the following fields:

\begin{itemize}
  \item a four-bit version number,
  \item a four-bit definition of the length of the header which \emph{must} be greater than or equal to five,
  \item a six-bit differentiated services code point,
  \item a two-bit explicit congestion notification,
  \item a sixteen-bit integer which defines the total length of \(p\),
  \item a sixteen-bit identification field,
  \item a zero bit,
  \item a bit which indicates whether or not \(p\) must not be fragmented, with a one indicating that the packet must \emph{not} be fragmented,
  \item a bit which indicates whether or not \(p\) is the last of a series of fragmented packets,
  \item a thirteen-bit fragment offset field,
  \item an eight-bit time-to-live field,
  \item an eight-bit value which identifies the payload protocol,
  \item a sixteen-bit checksum of the header,
  \item a thirty-two-bit IPv4 address, indicating the source of \(p\),
  \item a thirty-two-bit IPv4 address, indicating the destination of \(p\),
  \item an options field whose length is calculable, and
  \item a payload of calculable length.
\end{itemize}

A naive approach involves using \AgdaDatatype{Fin} for everything.  However, the author uses the term ``naive'' because the author prefers the alternative, which involves the use of more expressive datatypes which are specifically designed \emph{for} IPv4, although the \AgdaDatatype{Fin} approach really does work well for some fields.

\section{Addresses}
That at least two fields can be absent from the \AgdaField{Protocol.packetType} type may be immediately obvious; \AgdaRecord{Protocol} has native support for address schemes.  At this point, an IPv4 address type should probably be created, so the author \emph{has} gone and created such a type!  Specifically, the type is \AgdaFunction{Address}, which is defined as follows:

\begin{code}
  Address : Set
  Address = Fin (2 ^ 32)
\end{code}

The definition of \AgdaFunction{Address} follows pretty directly from the RFC's definition of IPv4 addresses; \AgdaDatatype{Fin} \AgdaSymbol(\AgdaNumber{2} \AgdaOperator{\AgdaFunction{\textasciicircum{}}} \AgdaBound{x}\AgdaSymbol) is the type of the \AgdaBound{x}-bit natural numbers.

\section{The Header Length Field}
Naively, one can say that the header length field is the combination of an appropriate \AgdaDatatype{Fin} number \(n\) and a proof which indicates that \(n \geq 5\).  In this case, the author actually \emph{likes} the naive approach.

\begin{code}
  IHL : Set
  IHL = Σ (Fin (2 ^ 4)) (\ n -> toℕ n ≥ 5)
\end{code}

\section{The Three Option Bits}
An approach to representing the three option bits which yields a not-particularly-readable result involves the use of \AgdaDatatype{Fin} \AgdaSymbol(\AgdaNumber{2} \AgdaOperator{\AgdaFunction{\textasciicircum{}}} \AgdaNumber{3}\AgdaSymbol).  However, the option bits an instead be represented as dedicated \AgdaDatatype{Bool} fields in a record type; this approach offers significantly more readability and prevents confusing the purposes of the individual option bits.

\subsection{IPv6-Specific Values}
Some \AgdaDatatype{PayloadProtocol} contructors, e.g., \AgdaInductiveConstructor{Ethernet}, are actually specific to IPv6.  However, IPv6 and IPv4 use the same IP protocol numbers, and the author does not believe that listing all such protocol numbers is in any real way problematic.

\section{The Complete Header Record}
Armed with the preceding information, a type \AgdaRecord{Header} can be decently easily defined such that for all \AgdaBound{p} of type \AgdaRecord{Header}, the following statements hold:

\begin{itemize}
  \item \AgdaField{Header.versionNumber} \AgdaBound{p} is the version number for the \AgdaField{p} packet.
  \item \AgdaField{Header.headerLength} \AgdaBound{p} is the combination of a four-bit number \(l\), which is the length of the header of the \AgdaBound{p} packet, and a value which guarantees that \(l \geq 5\).
  \item \AgdaField{Header.differentiatedServices} \AgdaBound{p} is the differentiated services code point for the \AgdaBound{p} packet.
  \item \AgdaField{Header.congestionNotification} \AgdaBound{p} is the explicit congestion notification for the \AgdaBound{p} packet.
  \item \AgdaField{Header.totalLength} \AgdaBound{p} is the total length of the \AgdaBound{p} packet.
  \item \AgdaField{Header.identification} \AgdaBound{p} is the raw content of the identification field of the \AgdaBound{p} packet.
  \item \AgdaField{Header.firstFlagBit} \AgdaBound{p} is \emph{reserved} and should be set to \AgdaInductiveConstructor{Fin.zero}.
  \item \AgdaField{Header.dontFragment} \AgdaBound{p} is true if and only if the \AgdaBound{p} packet \emph{must not} be fragmented into additional packets.
  \item \AgdaField{Header.moreFragments} \AgdaBound{p} is false if and only if the \AgdaBound{p} packet is the last of a series of fragmented packets or is a standalone packet.
  \item \AgdaField{Header.fragmentOffset} \AgdaBound{p}
  \item \AgdaField{Header.timeToLive} \AgdaBound{p}
  \item \AgdaField{Header.headerChecksum} \AgdaBound{p} is the checksum of the header of the \AgdaBound{p} packet.
  \item \AgdaField{Header.options} \AgdaBound{p} is the raw options field for the \AgdaBound{p} packet.
\end{itemize}

\begin{code}
  record Header (l : Level) : Set (Level.suc l) where
    field
      versionNumber : Fin (2 ^ 4)
      headerLength : IHL
      differentiatedServices : Fin (2 ^ 6)
      congestionNotification : Fin (2 ^ 2)
      totalLength : Fin (2 ^ 16)
      identification : Fin (2 ^ 16)
      firstOptionBit : Bool
      dontFragment : Bool
      moreFragments : Bool
      fragmentOffset : Fin (2 ^ 13)
      timeToLive : Fin (2 ^ 8)
      headerChecksum : Fin (2 ^ 16)
      options : Fin (2 ^ (toℕ (proj₁ headerLength) ∸ 5))
\end{code}

\section{The Protocol Record}
With this information, the IPv4 protocol can be considered to have the following characteristics:

\begin{itemize}
  \item The long name of IPv4 is ``Internet Protocol version 4''.
  \item IPv4 has a short name.  This short name is, obviously, ``IPv4''.
  \item IPv4 addresses are 32-bit numbers, which are represented by \AgdaFunction{Address}.
\end{itemize}

Accordingly, the IPv4 protocol can be defined with an \AgdaRecord{Core.Protocol} record as follows:

\begin{code}
  protocol : (l : Level) -> Core.Protocol (Level.suc l)
  protocol l = record
    {name = "Internet Protocol version 4"
    ;shortName = just "IPv4"
    ;addressType = just Address
    ;isSerialized = true
    ;headerType = Header l
    }
\end{code}

\section{The Addressful Packet Type}
Really, because \AgdaRecord{Core.Packet} exists, no explicit definition of a type for addressful IPv4 packets is necessary.  For any appropriate \AgdaBound{p}, \AgdaRecord{Core.Packet} \AgdaBound{p} is the type of packets which adhere to the \AgdaBound{p} protocol, and \AgdaFunction{protocol} is a description of the IPv4 protocol; therefore, \AgdaRecord{Core.Packet} \AgdaFunction{protocol} is the type of IPv4 packets.

\chapter{IPv6}
The situation with IPv4 is terrible; as of 2024, the address space has been exhausted for quite a while.  The address space for version 6 of the Internet Protocol, which is also known as ``IPv6'', is also finite but \emph{does} at least support significantly more addresses.  Because IPv6 is in some way superior to IPV4, this chapter defines a representation of IPv6; accordingly, \AgdaModule{Network.ProtocolsAndPackets} is now more capable than some Internet service providers.

\begin{code}
module IPv6 where
\end{code}

Much like with the definition of IPv4, the author begins with a look at the appropriate RFC, which, in this case, is RFC 8200.  RFC 8200 states that an IPv6 address is a 128-bit number and defines the structure of IPv4 packets, indicating that any IPv6 packet consists of the following values:

\begin{itemize}
  \item a four-bit version number,
  \item a six-bit differentiated services field,
  \item a two-bit Explicit Congestion Notification field,
  \item a twenty-bit flow label,
  \item a sixteen-bit number which indicates the length of the payload,
  \item an eight-bit value which specifies the type of the next header,
  \item an eight-bit hop limit,
  \item a source address, and
  \item a destination address.
\end{itemize}

\section{Addresses}
For reasons which should be obvious, the source address and destination address can be omitted from the custom header type but should probably be defined.  The author's definition of the IPv6 address type is as follows:

\begin{code}
  Address : Set
  Address = Fin (2 ^ 128)
\end{code}

\section{The Header Type}
For a given value \AgdaBound{h} of type \AgdaRecord{Header} \AgdaBound{l}, the following statements hold:

\begin{itemize}
  \item \AgdaField{Header.versionNumber} \AgdaBound{h} is the version number of the \AgdaBound{h} packet.
  \item \AgdaField{Header.differentiatedServices} \AgdaBound{h} is a straightforward representation of the differentiated services field of the \AgdaBound{h} packet.
  \item \AgdaField{Header.congestionNotification} \AgdaBound{h} is a straightforward representation of the Explicit Congestion Notification field of the \AgdaBound{h} packet.
  \item \AgdaField{Header.flowLabel} \AgdaBound{h} is a straightforward representation of the flow label of the \AgdaBound{h} packet.
  \item \AgdaField{Header.payloadLength} \AgdaBound{h} is the length of the payload of the \AgdaBound{h} packet.
  \item \AgdaField{Header.protocol} \AgdaBound{h} indicates the protocol of the payload of the \AgdaBound{h} packet.  Much like with \AgdaFunction{IPv4.Header}, to represent the next header field, \AgdaRecord{Protocol} can be used instead of a dedicated type or simple \AgdaDatatype{Fin} approach.
  \item \AgdaField{Header.hopLimit} \AgdaBound{h} is the hop limit of the \AgdaBound{h} packet.
\end{itemize}

\begin{code}
  record Header (l : Level) : Set (Level.suc l) where
    field
      versionNumber : Fin (2 ^ 4)
      differentiatedServices : Fin (2 ^ 6)
      congestionNotification : Fin (2 ^ 2)
      flowLabel : Fin (2 ^ 20)
      payloadLength : Fin (2 ^ 16)
      protocol : Core.Protocol l
      hopLimit : Fin (2 ^ 8)
\end{code}

\section{The Protocol Record}
Given this information and other information in the appropriate RFC, one can safely argue that the IPv6 protocol has the following characteristics:

\begin{itemize}
  \item The long name of IPv6 is ``Internet Protocol version 6''.
  \item The short name of IPv6 is, obviously, ``IPv6''.
  \item IPv6 addresses are \AgdaFunction{Address} values.
  \item IPv6 is serialized.
  \item IPv6 headers can be represented by \AgdaRecord{Header} values.
\end{itemize}

Accordingly, IPv6 can be described as follows:

\begin{code}
  protocol : (l : Level) -> Core.Protocol (Level.suc l)
  protocol l = record
    {name = "Internet Protocol version 6"
    ;shortName = just "IPv6"
    ;addressType = just Address
    ;isSerialized = true
    ;headerType = Header l
    }
\end{code}

\section{The Packet Type}
Defining the IPv6 packet type is left as an exercise for the reader.

\chapter{ICMP}
This chapter describes the Internet Control Message Protocol, which is also defined in RFC 792.

\begin{code}
module ICMP where

  open AdditionalFinPatterns
\end{code}

\section{The Generic ICMP Representation}
RFC 792 indicates that the following statements apply:

\begin{itemize}
  \item ICMP's name is ``Internet Control Message Protocol''.
  \item The short name of ICMP is, obviously, ``ICMP''.
  \item Parasitizing IPv4, ICMP does not necessarily use addresses.
  \item ICMP does serialization.
  \item None of the boilerplate headers really apply to non-serialized representations of packets.
\end{itemize}

Given the preceding information, one can reasonably write the following ICMP representation:

\begin{code}
  carrierProtocol : Core.Protocol Level.zero
  carrierProtocol = record
    {name = "Internet Control Message Protocol"
    ;shortName = just "ICMP"
    ;addressType = nothing
    ;isSerialized = true
    ;headerType = ⊤
    }
\end{code}

However, as indicated by the use of \AgdaDatatype{⊤}, this record is not terribly useful alone.  Luckily, \AgdaRecord{Core.UPacket} values can contain payloads of a given protocol, and the various ICMP message types can be thought of as actually being different protocols.

\section{The Subprotocols}
ICMP messages can be thought of as being a collection of a carrier protocol and a bunch of ``subprotocols'', where a subprotocol just defines some additional header fields and the structure of the content of the ICMP message.  This paper uses this approach.

\subsection{Boilerplate Records which are Not Directly Subprotocols}
Some patterns of fields are frequent within different ICMP subprotocols.  For the sake of reducing the overall length of the ICMP definition and facilitating actually working with \AgdaModule{ICMP}, these frequently-used fields are defined alone in the \AgdaModule{Boilerplate} submodule.

\begin{code}
  module Boilerplate where

    record IdSequence : Set where
      field
        identifier : Fin (2 ^ 16)
        sequenceNumber : Fin (2 ^ 16)

    record IPv4Context (l : Level) : Set (Level.suc l) where
      field
        header : IPv4.Header l
        first8Bytes : Fin (2 ^ (8 * 8))
\end{code}

\subsection{Subprotocols and Supporting Functions}

\begin{code}
  module Subprotocol (l : Level) where
\end{code}

\subsubsection{The Subprotocol-Type-to-Protocol Function}
Pretty much every subprotocol has the same repetitive \AgdaRecord{Core.Protocol} attributes; accordingly, to reduce the amount of boilerplate garbage, the author has defined \AgdaFunction{withAttributes}, which converts a given ICMP subprotocol header-and-content type into a usable representation of the subprotocol.

\begin{code}
    withAttributes : {l : Level} -> String -> Set l -> Core.Protocol l
    withAttributes name content = record
      {name = name
      ;shortName = nothing
      ;addressType = nothing
      ;isSerialized = true
      ;headerType = content
      }
\end{code}

\subsubsection{The Supporting ``Request or Reply'' Type}
There exist ICMP message structures \(\mathrm{thing}_1\) and \(\mathrm{thing}_2\) such that the overall structure and semantics of \(\mathrm{thing}_1\) are the overall structure and semantics of \(\mathrm{thing}_2\), but \(\mathrm{thing}_1\) and \(\mathrm{thing}_2\) represent requests and responses, respectively.  Instead of defining separate representations for requests and replies, by using a relevant type, subprotocol representations can instead indicate whether the message is a request or a response.  \AgdaDatatype{RequestOrReply} is such a type.  \AgdaInductiveConstructor{Request} and \AgdaInductiveConstructor{Reply} indicate, as expected, requests and replies, respectively.

\begin{code}
    data RequestOrReply : Set where
      Request : RequestOrReply
      Reply : RequestOrReply
\end{code}

Really, this type is just a differently-named boolean type.  However, the different naming scheme offers additional expressiveness and enables the reasonable use of simpler field names.

\subsubsection{Subprotocol for Timestamp Messages}
Timestamp request messages and timestamp reply messages use the same subprotocol format, which is characterized by having the following fields:

\begin{itemize}
  \item a message identifier,
  \item a sequence number,
  \item an originate timestamp,
  \item a receive timestamp, and
  \item a transmit timestamp.
\end{itemize}

All timestamps are thirty-two-bit values.  Also, timestamp requests and timestamp replies have different message types.

\AgdaRecord{Boilerplate.IdSequence} can be easily used to represent the first two fields.  Using a basic \AgdaDatatype{Fin} type, the representation of timestamps is pretty straightforward, too.  \AgdaDatatype{RequestOrReply} lends well to indicating whether the message is a request or a reply.  With this information, a type \AgdaRecord{Timestamp.Content} for ICMP's timestamp-centric messages can be created as follows:

\begin{code}
    module Timestamp where
      record Content : Set (Level.suc l) where
        field
          header : Boilerplate.IdSequence
          requestOrReply : RequestOrReply
          originate
           receive
           transmit : Fin (2 ^ 32)
        private
          levelPadding = tt
\end{code}

With \AgdaFunction{withAttributes}, converting the record type into a usable subprotocol is pretty easy:

\begin{code}
      protocol : Core.Protocol (Level.suc l)
      protocol = withAttributes "Subprotocol for timestamp-centric ICMP \
                                \messages"
                                Content
\end{code}

\section{Subprotocol for Messages about Address Masks}
The relevant RFC indicates that an address mask request or reply consists of the following additional values:

\begin{itemize}
  \item the \AgdaRecord{Boilerplate.IdSequence} values and
  \item a thirty-two-bit address mask.
\end{itemize}

Accordingly, representation is pretty straightforward:

\begin{code}
    module AddressMask where
      record Content : Set (Level.suc l) where
        field
          header : Boilerplate.IdSequence
          requestOrReply : RequestOrReply
          addressMask : Fin (2 ^ 32)
        private
          levelPadding = tt

      protocol : Core.Protocol (Level.suc l)
      protocol = withAttributes "Subprotocol for ICMP's address mask messages"
                                Content
\end{code}

\subsubsection{Subprotocol for Messages about Exceeded Time}
The RFC indicates that messages about exceeded time really only have two additional fields, which are an IPv4 header and the first eight bytes of the corresponding payload; these values provide context for the message.  As indicated by the use of the term ``context'', \AgdaRecord{Boilerplate.IPv4Context} is very useful here and makes quick work of this packet structure:

\begin{code}
    module TimeExceeded where
      record Content : Set (Level.suc l) where
        field
          context : Boilerplate.IPv4Context l

      protocol : Core.Protocol (Level.suc l)
      protocol = withAttributes "Subprotocol for ICMP messages about exceeded \
                                \time"
                                Content
\end{code}

\subsubsection{Subprotocol for Source Quench Messages}
The RFC indicates that source quench messages just provide context for the message, so representation is pretty simple:

\begin{code}
    module SourceQuench where
      record Content : Set (Level.suc l) where
        field
          context : Boilerplate.IPv4Context l

      protocol : Core.Protocol (Level.suc l)
      protocol = withAttributes "Subprotocol for when the router says \
                                \\"SHUT UP!\""
                                Content
\end{code}

\subsubsection{Subprotocol for Redirection Messages}
The RFC indicates that ICMP redirect messages have the beloved boilerplate context fields and an IPv4 address.  Accordingly, the definition is easy:

\begin{code}
    module Redirect where
      record Content : Set (Level.suc l) where
        field
          address : IPv4.Address
          context : Boilerplate.IPv4Context l

      protocol : Core.Protocol (Level.suc l)
      protocol = withAttributes "Subprotocol for ICMP's redirection messages"
                                Content
\end{code}

\subsubsection{Subprotocol for Unreachability Notifications}
The RFC indicates that ICMP unreachability notices have the IPv4 context field and a sixteen-bit MTU.  The definition is straightforward:

\begin{code}
    module Unreachable where
      record Content : Set (Level.suc l) where
        field
          nextHopMtu : Fin (2 ^ 16)
          context : Boilerplate.IPv4Context l

      protocol : Core.Protocol (Level.suc l)
      protocol = withAttributes "Subprotocol for ICMP's messages regarding \
                                \unreachability"
                                Content
\end{code}

\subsubsection{The Generic Fallback Pseudo-Subprotocol}
The subprotocol types which have so far been provided do not account for every possible ICMP message; as such, a generic fallback type for subprotocols is necessary.  In this case, the type is \AgdaRecord{Unsupported.Content}, which can --- with great ease --- represent any possible ICMP message.  However, \AgdaRecord{Unsupported.Content} offers no real restrictions or expressiveness, and, as such, the author recommends using the \AgdaModule{Unsupported} representations only if using any other representation is infeasible.

Anyway, back to the point, the definition is as follows:

\begin{code}
    module Unsupported where
      record Content : Set (Level.suc l) where
        field
          type : Fin (2 ^ 8)
          code : Fin (2 ^ 8)
          checksum : Fin (2 ^ 16)
          rawContent : Σ ℕ Fin
        private
          levelPadding = tt

      protocol : Core.Protocol (Level.suc l)
      protocol = withAttributes "Pseudo-subprotocol for ICMP which \
                                \represents an unsupported subprotocol"
                                Content
\end{code}

A value \AgdaBound{u} of type \AgdaRecord{Content} should satisfies the following statements:

\begin{itemize}
  \item \AgdaField{Content.type} \AgdaBound{u} is the value of the type field of the \AgdaBound{u} message.
  \item \AgdaField{Content.code} \AgdaBound{u} is the value of the code field of the \AgdaBound{u} message.
  \item \AgdaField{Content.checksum} \AgdaBound{u} is the value of the checksum field of the version of the \AgdaBound{u} message whose checksum is just zero.
  \item \AgdaField{Content.rawContent} \AgdaBound{u} is the serialized representation of the rest of the \AgdaBound{u} message.
\end{itemize}

\section{Renaming the Generic ICMP Record}
To prevent name clashes, the original name of the ICMP representation is ``\AgdaFunction{carrierProtocol}''.  However, this name conflicts with the names of other protocols; the standard name is just ``\AgdaFunction{protocol}''.  Accordingly, to increase the ease of referring to ICMP, the author creates the following alias for the ICMP representation:

\begin{code}
  protocol : Core.Protocol Level.zero
  protocol = carrierProtocol
\end{code}
\end{document}
